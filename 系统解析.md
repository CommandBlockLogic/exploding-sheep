# 羊羊系统解析

## 第一部分 羊羊的发射、飞行、碰撞与爆炸

这部分内容为了叙述方便，简化掉了实际数据包中一些对核心系统无关紧要的内容。在这部分最后一章会简略叙述。

### 一、羊羊的飞行物理引擎

由于MC自带的Motion计算了空气阻力，且这个空气阻力大的一批，导致难以预测羊羊飞行轨迹，给玩家游戏体验造成很大影响。因此觉得动手造轮子，忽略空气阻力，让羊羊的飞行轨迹完全符合抛物线，提高游戏体验。

知道一个物体速度和加速度随时间的变化，就可以完全描述这个物体的运动情况。建立六个计分板，分别是`s_vx`、`s_vy`、`s_vz`、`s_ax`、`s_ay`和`s_az`，表示羊羊在三个方向上的速度和加速度。速度单位是毫块每游戏刻(`milliBlock/tick`)，类比于米每秒(`m/s`)。加速度单位是毫块每平方游戏刻(`milliBlock/(tick^2)`)，类比于米每平方秒(`m/(s^2)`)，这里用0.001倍的单位是为了提高精度。注：第一部分所用计分板均以`s_`开头，以便区分。

由于MC世界是个纯重力场，因此`s_ax`和`s_az`均为零，`s_ay`为重力加速度。经过我的测试，这里`s_ay`取`-50 milliBlock/(tick^2)`较为合适。注：若想在某区域产生其他力场如任意方向的风力场，可通过修改该区域内羊羊的加速度完成。

创建一个函数`sheep:fly/move`来实现羊羊的飞行。该函数由每个羊羊高频执行。

```
# 设置每只羊羊的加速度
scoreboard players set @s s_ax 0
scoreboard players set @s s_ay -50
scoreboard players set @s s_az 0
# 再使羊羊的速度变化
scoreboard players operation @s s_vx += @s s_ax
scoreboard players operation @s s_vy += @s s_ay
scoreboard players operation @s s_vz += @s s_az

# 羊羊的坐标变化
# x
execute store result score @s s_temp1 run data get entity @s Pos[0] 1000
scoreboard players operation @s s_temp1 += @s s_vx
execute store result entity @s Pos[0] double 0.001 run scoreboard players get @s s_temp1
# y
execute store result score @s s_temp1 run data get entity @s Pos[1] 1000
scoreboard players operation @s s_temp1 += @s s_vy
execute store result entity @s Pos[1] double 0.001 run scoreboard players get @s s_temp1
# z
execute store result score @s s_temp1 run data get entity @s Pos[2] 1000
scoreboard players operation @s s_temp1 += @s s_vz
execute store result entity @s Pos[2] double 0.001 run scoreboard players get @s s_temp1
```

接下来新建一个函数来测试生成一只羊羊：
```
# 生成羊羊 此处对羊羊的设置有：小羊、静音、无敌和无AI。加上sheep_new的tag用于后续设置速度。
# 此处加入sheep_sheep的tag便于上一个高频飞行函数的执行，防止影响到世界中其他无辜的羊羊
summon minecraft:sheep ~ ~ ~ {Tags:["sheep_sheep","sheep_new"],Color:0,Silent:1,Age:-999999,Invulnerable:1,NoAI:1}
# 设置新羊羊的初速度
scoreboard players set @e[tag=sheep_new,limit=1] s_vx 100
scoreboard players set @e[tag=sheep_new,limit=1] s_vy 100
scoreboard players set @e[tag=sheep_new,limit=1] s_vz 0
# 清除新羊的tag
tag @e[tag=sheep_new] remove sheep_new
```
执行该函数，就会发现一只羊羊划过完美的抛物线，最后坠入虚空。


### 二、羊羊的碰撞判定

为了防止羊羊坠入虚空，使其停在地面上，就要对羊羊碰撞方块进行判定。羊羊是一个具有碰撞箱的生物，不能简单的用一个点来判断是否碰撞。这里我用包围这只羊的长方体的8个顶点作为碰撞判断的点，若这八个点里有任意一个点碰到方块，则视为整只羊碰到了方块。除了判定碰到方块，还要判定是在哪个方向上碰到的方块，便于后续让羊停下来。因此，我采取了一个方法：先让羊向x方向移动，若此时碰到方块，则说明是碰到了x方向上的方块；接着让羊向y方向移动，判定碰撞；再向z方向移动，接着判定。这样，在哪个方向上碰到了方块，就很明确了。

在判定到在特定方向上碰撞方块后，需要对羊羊的运动进行处理。若碰到一个方向的方块，那就让羊在这个方向的速度变为零，这样它就不会再向这个方向前进了，也就做到了碰方块停止的效果。但这样还不够，碰撞必定带来能量损失，也就是速度的降低。在碰撞方向的速度变为零后，其余两个方向的速度应该也有所降低。

下面是加上碰撞判定后的`sheep:fly/move`：

```
# 前半部分和原来一样
# 设置每只羊羊的加速度
scoreboard players set @s s_ax 0
scoreboard players set @s s_ay -50
scoreboard players set @s s_az 0
# 再使羊羊的速度变化
scoreboard players operation @s s_vx += @s s_ax
scoreboard players operation @s s_vy += @s s_ay
scoreboard players operation @s s_vz += @s s_az

# 羊羊的坐标变化
# 此处羊羊的位置变化也和原来一样，在位置变化后加入了碰撞判定
# x
execute store result score @s s_temp1 run data get entity @s Pos[0] 1000
scoreboard players operation @s s_temp1 += @s s_vx
execute store result entity @s Pos[0] double 0.001 run scoreboard players get @s s_temp1
# 碰撞判定 此处把碰撞后的速度变化放到了函数sheep:fly/touch_x中
# 此处tag=!sheep_touch_x的作用是，在一个点碰撞x方向一次后，其他碰撞点就不能继续碰撞，以免进行多余操作
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x

# y
execute store result score @s s_temp1 run data get entity @s Pos[1] 1000
scoreboard players operation @s s_temp1 += @s s_vy
execute store result entity @s Pos[1] double 0.001 run scoreboard players get @s s_temp1
# 碰撞判定
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y

# z
execute store result score @s s_temp1 run data get entity @s Pos[2] 1000
scoreboard players operation @s s_temp1 += @s s_vz
execute store result entity @s Pos[2] double 0.001 run scoreboard players get @s s_temp1
# 碰撞判定
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z

# 清除tag
tag @s[tag=sheep_touch_x] remove sheep_touch_x
tag @s[tag=sheep_touch_y] remove sheep_touch_y
tag @s[tag=sheep_touch_z] remove sheep_touch_z
```

以x方向为例，碰撞后的速度变化在函数`sheep:fly/touch_x`中：
```
# 让羊移动回运动前的位置，避免嵌到方块里
execute store result score @s s_temp1 run data get entity @s Pos[0] 1000
scoreboard players operation @s s_temp1 -= @s s_vx
execute store result entity @s Pos[0] double 0.001 run scoreboard players get @s s_temp5

# 将x方向的速度变为0
scoreboard players set @s s_vx 0
# 使z方向上的速度减小70%
scoreboard players set @s s_temp1 7
scoreboard players operation @s s_vz *= @s s_temp5
scoreboard players set @s s_temp1 10
scoreboard players operation @s s_vz /= @s s_temp5

# 加上碰撞x方向的tag
tag @s[tag=!sheep_touch_x] add sheep_touch_x
```

`sheep:fly/touch_y`和`sheep:fly/touch_z`同理。此处对碰撞后速度的处理是模拟的完全非弹性碰撞，若想要实现弹性碰撞，可将对应方向的速度反向再加衰减。

再次执行第一章的发射羊羊的函数，可以看到羊羊落在了地面上。


### 三、羊羊的发射

游戏中，每个玩家都可以朝面前发射羊羊。这里的发射就是让羊羊获得向目前玩家面向方向的初速度。

把上面发射羊羊的函数进行修改，新建函数`sheep:launch/main`，该函数由玩家执行：
```
# 生成羊羊
summon minecraft:sheep ~ ~ ~ {Tags:["sheep_sheep","sheep_new"],Color:0,Silent:1,Age:-999999,Invulnerable:1,NoAI:1}
# 在玩家面前一个生成一个标记，用来计算初速度
summon minecraft:area_effect_cloud ^ ^ ^1 {Tags:["sheep_mark"]}

# 计算羊羊的初速度 这里用玩家面前标记的坐标减去羊羊的坐标就可以得到对应方向的初速度
# x
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp1 run data get entity @s Pos[0] 1000
execute as @e[tag=sheep_new,limit=1] store result score @s s_temp1 run data get entity @s Pos[0] 1000
scoreboard players operation @e[tag=sheep_mark,limit=1] s_temp1 -= @e[tag=sheep_new,limit=1] s_temp1
execute store result score @e[tag=sheep_new,limit=1] s_vx run scoreboard players get @e[tag=sheep_mark,limit=1] s_temp1
# y
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp1 run data get entity @s Pos[1] 1000
execute as @e[tag=sheep_new,limit=1] store result score @s s_temp1 run data get entity @s Pos[1] 1000
scoreboard players operation @e[tag=sheep_mark,limit=1] s_temp1 -= @e[tag=sheep_new,limit=1] s_temp1
execute store result score @e[tag=sheep_new,limit=1] s_vy run scoreboard players get @e[tag=sheep_mark,limit=1] s_temp1
# z
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp1 run data get entity @s Pos[2] 1000
execute as @e[tag=sheep_new,limit=1] store result score @s s_temp1 run data get entity @s Pos[2] 1000
scoreboard players operation @e[tag=sheep_mark,limit=1] s_temp1 -= @e[tag=sheep_new,limit=1] s_temp1
execute store result score @e[tag=sheep_new,limit=1] s_vz run scoreboard players get @e[tag=sheep_mark,limit=1] s_temp1

# 清除新羊的tag并删除标记
kill @e[tag=sheep_mark]
tag @e[tag=sheep_new] remove sheep_new
```

由玩家执行该函数，就可以看到一只羊羊朝自己面前发射出去了。如果想提高羊羊的飞行速度，简便的方法是增加生成的标记和玩家之间的距离，这样初速度就会成倍增加了。

### 四、羊羊的碰撞判定优化

在发射羊羊的函数中，如果把羊的初速度调的太高，就会发生明明还没有碰到墙壁就停止运动的情况。这是由于运动速度太快，一游戏刻内判定一次碰撞精度太低。即便初速度不快，在第一章中，说到羊羊世界重力加速度是-50毫块每平方游戏刻(`-50 milliBlock/(tick^2)`)，因此，在1秒后也就是20游戏刻后，羊羊在y轴的速度改变量将达到`-50*20=-1000milliBlock/tick`。也就是说，羊羊将在1游戏刻内下落完整的一格。自然下落所增加的速度也是相当可观的。因此，提高碰撞判定精度迫在眉睫。为了增加碰撞判定精度，我在这里选择在1游戏刻内进行5次碰撞判定，也就是说，把1游戏刻分为5部分，羊羊每次前进其速度的1/5，每次前进后进行一次判定。为此，对`sheep:fly/`中的函数进行改写。

新建一个函数`sheep:fly/split_speed`，将速度除以5：
```
# 把羊羊在每个方向上的速度都除以5，分别存到s_temp2、s_temp3、s_temp4上
scoreboard players set @s s_temp1 5
execute store result score @s s_temp2 run scoreboard players get @s s_vx
execute store result score @s s_temp3 run scoreboard players get @s s_vy
execute store result score @s s_temp4 run scoreboard players get @s s_vz
scoreboard players operation @s s_temp2 /= @s s_temp1
scoreboard players operation @s s_temp3 /= @s s_temp1
scoreboard players operation @s s_temp4 /= @s s_temp1
```

再新建一个函数`sheep:fly/tick`，承载五次移动碰撞判定，把设置加速度的部分挪到这里：
```
# 设置每只羊羊的加速度
scoreboard players set @s s_ax 0
scoreboard players set @s s_ay -50
scoreboard players set @s s_az 0
# 再使羊羊的速度变化
scoreboard players operation @s s_vx += @s s_ax
scoreboard players operation @s s_vy += @s s_ay
scoreboard players operation @s s_vz += @s s_az

# 把速度除以5
function sheep:fly/split_speed

# 进行五次移动和碰撞判定
function sheep:fly/move
function sheep:fly/move
function sheep:fly/move
function sheep:fly/move
function sheep:fly/move
```

对`sheep:fly/move`的内容稍稍进行改写，去掉前面设置加速度的部分，并把`s_vx`、`s_vy`和`s_vz`替换成`s_temp2`、`s_temp3`和`s_temp4`。

`sheep:fly/move`：
```
# x方向
execute store result score @s s_temp5 run data get entity @s Pos[0] 1000
scoreboard players operation @s s_temp5 += @s s_temp2
execute store result entity @s Pos[0] double 0.001 run scoreboard players get @s s_temp5
# 碰撞判定
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x
execute at @s[tag=!sheep_touch_x] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_x

# y
execute store result score @s s_temp1 run data get entity @s Pos[1] 1000
scoreboard players operation @s s_temp1 += @s s_temp3
execute store result entity @s Pos[1] double 0.001 run scoreboard players get @s s_temp1
# 碰撞判定
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y
execute at @s[tag=!sheep_touch_y] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_y

# z
execute store result score @s s_temp1 run data get entity @s Pos[2] 1000
scoreboard players operation @s s_temp1 += @s s_temp4
execute store result entity @s Pos[2] double 0.001 run scoreboard players get @s s_temp1
# 碰撞判定
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~0.4 ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~ ~0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~0.4 ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z
execute at @s[tag=!sheep_touch_z] positioned ~-0.3 ~ ~-0.3 unless block ~ ~ ~ minecraft:air run function sheep:fly/touch_z

# 清除tag
tag @s[tag=sheep_touch_x] remove sheep_touch_x
tag @s[tag=sheep_touch_y] remove sheep_touch_y
tag @s[tag=sheep_touch_z] remove sheep_touch_z
```

同理，`sheep:fly/touch_x`中也需要替换和改写：
```
# 让羊移动回运动前的位置，避免嵌到方块里
execute store result score @s s_temp1 run data get entity @s Pos[0] 1000
scoreboard players operation @s s_temp1 -= @s s_temp2
execute store result entity @s Pos[0] double 0.001 run scoreboard players get @s s_temp5

# 将x方向的速度变为0
scoreboard players set @s s_vx 0
# 使z方向上的速度减小70%
scoreboard players set @s s_temp1 7
scoreboard players operation @s s_vz *= @s s_temp5
scoreboard players set @s s_temp1 10
scoreboard players operation @s s_vz /= @s s_temp5

# 把改变后的速度除以5
function sheep:fly/split_speed

# 加上碰撞x方向的tag
tag @s[tag=!sheep_touch_x] add sheep_touch_x
```

经过以上的改写过程，碰撞精度会大大提高，不会出现没碰到墙就停下来的情况。

### 五、羊羊的爆炸触发

在游戏过程中，羊羊在碰到方块一段时间后会触发爆炸。这里使用了一个计分板`s_blow_time`，碰到方块后置为1，12游戏刻后发生爆炸。

首先修改`sheep:fly/move`，增加爆炸触发，在清除tag前增加以下三行：
```
scoreboard players set @s[tag=sheep_touch_x,scores={s_blow_time=0}] s_blow_time 1
scoreboard players set @s[tag=sheep_touch_y,scores={s_blow_time=0}] s_blow_time 1
scoreboard players set @s[tag=sheep_touch_z,scores={s_blow_time=0}] s_blow_time 1
```
再修改`sheep:launch/main`，初始化`s_blow_time`，在清除tag前增加：
```
scoreboard players set @e[tag=sheep_new] s_blow_time 0
```

新建`sheep:blow/tick`，高频运行：
```
scoreboard players add @e[scores={s_blow_time=1..}] s_blow_time 1
execute as @e[scores={s_blow_time=13..}] at @s run function sheep:blow/main
```

新建`sheep:blow/main`：
```
summon minecraft:creeper ~ ~ ~ {Fuse:0,Invulnerable:1,Tags:["sheep_blower"],ExplosionRadius:2}
kill @s
```

如此这般，羊羊在碰撞方块后便会爆炸。

### 六、实际数据包中存在的优化修改情况

以上四章已经基本实现游戏中羊羊的物理引擎。但实际游戏中还有一些基于游戏特性的修改。

#### 发射的羊羊种类多

游戏中存在多种羊羊，其行为均有所不同。例如黄羊无重力飞行，在`sheep:fly/tick`中将`s_ay`设为0；红羊落地立刻爆炸，在`sheep:blow/tick`中进行调整；冰羊、橙羊、黑羊爆炸效果不同在`sheep:blow/main`中单独设置。

为了配合多种羊羊，`sheep:launch/main`也有所修改。

#### 为死亡信息服务

为了做出自己的击杀信息，我为每个玩家分配了玩家id，在`sheep:launch/main`中把玩家id复制给了羊羊，羊羊从此之后有了主人。在`sheep:blow/main`中，在即将爆炸的羊羊附近把自己主人的id复制给了周围爆炸范围内的玩家，则可得到玩家的死因。

#### 其他

还有很多修改没有讲到，如终极羊羊的释放。但是以上内容已经是数据包中羊羊部分的主要架构，其余部分可自行分析。

## 第二部分 芭蕉扇

### 一、推动玩家

芭蕉扇推玩家的原理很简单，就是碰撞箱+失重。给玩家几个tick的失重的同时用超过20只隐形的猪来推动玩家。但由于玩家的惯性，在玩家运动的时候推动方向可能和想象的不一样。首先新建函数`sheep:fan/main`，在玩家挥扇子时执行。在这个函数里，要首先确定推动了哪个玩家，并给这个玩家一个推力。该函数如下：
```
#标记挥扇子的玩家
tag @s add sheep_fanning
#标记被扇的玩家
execute at @s positioned ~ ~1 ~ positioned ^ ^ ^3.5 as @a[distance=..4.5,tag=!sheep_fanning] at @s anchored eyes run function sheep:fan/player
```

函数中可以看到，为了简化（懒），扇子的范围是玩家面前的球形。在被扇玩家和挥扇子玩家连线上生成推进器来推动玩家。关键函数`sheep:fan/player`如下：
```
#失重效果
effect give @s minecraft:levitation 1 20 true
scoreboard players set @s s_push_time 3

#生成推进器
execute facing entity @a[tag=sheep_fanning,limit=1] eyes run summon minecraft:pig ^ ^ ^0.4 {Tags:["sheep_pusher"],Silent:1,NoAI:1,Health:1}
execute facing entity @a[tag=sheep_fanning,limit=1] eyes run summon minecraft:pig ^ ^ ^0.4 {Tags:["sheep_pusher"],Silent:1,NoAI:1,Health:1}
execute facing entity @a[tag=sheep_fanning,limit=1] eyes run summon minecraft:pig ^ ^ ^0.4 {Tags:["sheep_pusher"],Silent:1,NoAI:1,Health:1}
#...后面还有17条一样的就不写了

#删除推进器
tp @e[tag=sheep_pusher] ~ -100 ~
```

在`sheep:fan/tick`函数中删除玩家的失重效果：
```
scoreboard players remove @e[scores={s_push_time=1..}] s_push_time 1
effect clear @e[scores={s_push_time=0}] minecraft:levitation
```

### 二、推动羊羊

首先标记能够被推动的羊羊，在`sheep:fan/main`中添加一行：
```
execute at @s positioned ~ ~1 ~ positioned ^ ^ ^3.5 run tag @e[tag=sheep_sheep,tag=!sheep_fanned,distance=..4.5] add sheep_fanned
```

羊羊移动直接改计分板就好啦，首先确定风的推力，在挥扇子玩家身上用`s_temp2`、`s_temp3`和`s_temp4`保存风力，最后直接修改羊羊的速度，在`sheep:fan/main`中添加：
```
#生成标记
summon minecraft:area_effect_cloud ^ ^ ^-1.1 {Tags:["sheep_mark"]}
#保存风力
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp2 run data get entity @s Pos[0] 1000
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp3 run data get entity @s Pos[1] 1000
execute as @e[tag=sheep_mark,limit=1] store result score @s s_temp4 run data get entity @s Pos[2] 1000
execute store result score @s s_temp2 run data get entity @s Pos[0] 1000
execute store result score @s s_temp3 run data get entity @s Pos[1] 1000
execute store result score @s s_temp4 run data get entity @s Pos[2] 1000
scoreboard players operation @s s_temp2 -= @e[tag=sheep_mark,limit=1] s_temp2
scoreboard players operation @s s_temp3 -= @e[tag=sheep_mark,limit=1] s_temp3
scoreboard players operation @s s_temp4 -= @e[tag=sheep_mark,limit=1] s_temp4
#羊羊被吹
execute as @e[tag=sheep_fanned] at @s run scoreboard players operation @s s_vx += @a[tag=sheep_fanning,limit=1] s_temp2
execute as @e[tag=sheep_fanned] at @s run scoreboard players operation @s s_vy += @a[tag=sheep_fanning,limit=1] s_temp3
execute as @e[tag=sheep_fanned] at @s run scoreboard players operation @s s_vz += @a[tag=sheep_fanning,limit=1] s_temp4
```

### 三、推动怪物

推动怪物为了达到较好的效果，就直接把范围内怪物的动量修改为风的动量。在`sheep:fan/main`中添加（怪物默认tag=monster）：
```
execute at @s positioned ~ ~1 ~ positioned ^ ^ ^3.5 run tag @e[tag=monster,distance=..4.5] add sheep_fanned_monster
execute as @e[tag=sheep_fanned_monster] store result entity @s Motion[0] double 0.001 run scoreboard players get @a[tag=sheep_fanning,limit=1] s_temp2
execute as @e[tag=sheep_fanned_monster] store result entity @s Motion[1] double 0.001 run scoreboard players get @a[tag=sheep_fanning,limit=1] s_temp3
execute as @e[tag=sheep_fanned_monster] store result entity @s Motion[2] double 0.001 run scoreboard players get @a[tag=sheep_fanning,limit=1] s_temp4
```

## 第三部分 游戏流程系统实现

### 一、游戏流程控制

为了简化游戏流程处理，可以把整个游戏流程分为多个阶段（如等待阶段、预备阶段、游戏进行阶段等）。每个阶段有两个最主要的函数：初始化函数和高频执行函数。每从一个阶段进入另一个阶段时，执行新阶段的初始化函数并在整个阶段的持续时间内执行高频函数，即可达成简单的流程控制。

游戏内通过一个计分板数值(system计分板的game_status值)来确定当前地图所处阶段。通过这个计分板在`minecraft:tick`中对各个阶段的高频函数进行执行。

以个人死斗模式为例：

首先玩家进入地图处于自由阶段，对应game_status值为0。这个阶段的高频函数内容包括选队伍、选羊羊类型、pve模式等等。玩家选择进入游戏，人数判断通过后就会执行游戏预备阶段的初始化函数，即`minecraft:game_mode/single/death_match/pre/main`。这个初始化函数的主要任务是修改game_status值为103，标记进入该阶段，初始化玩家及地图等。这个阶段结束后进入个人死斗模式的正式阶段，执行`minecraft:game_mode/single/death_match/start`，修改game_status值为3，标记进入正式阶段，同时传送玩家等。在正式阶段分出胜负后，进入游戏结束阶段。执行`minecraft:game_mode/ending/main`，修改game_status值为8964，结束游戏进程，倒计时回到自由阶段。

以上流程有一些显而易见的优点：结构简单明确，同时复用性强。初始化、清空等相同操作均放在`minecraft:game_mode/public/`下进行复用，给新增游戏模式和维护带来极大便利。

同时这种流程也有缺点，如果说游戏阶段多，整个流程会很复杂，来回跳转不清晰，很容易乱。不过羊羊还好，完全不复杂，很好用。

### 二、死亡提示

羊羊游戏有个特点，就是伤害来源都是Creeper，如果没有击杀反馈，游戏体验会变得很差。确定玩家是被谁炸死的就很重要。但是除了被炸死之外，玩家还有可能被摔死，怪物锤死等等，如何区分开这些也是一大难点。

#### 被羊羊炸死

首先要判定的就是被某人丢出的羊羊炸死。首先新增两个个计分板`s_id`和`s_thrower_id`分别记录玩家的id和本羊羊是哪个玩家丢出的（这里分配玩家id不讲了好吧）。在`sheep:launch/main`中添加一行：
```
execute store result score @e[tag=sheep_new,limit=1] s_thrower_id run scoreboard players get @s s_id
```

知道羊是谁丢的之后还要知道它炸了谁。接着新增一个计分板`s_kill_time`记录玩家上次被炸的时间，在`sheep:blow/main`中添加如下几条
```
#爆炸四格范围内的玩家都被选中
execute at @s as @a[distance=..4] store result score @s s_thrower_id run scoreboard players get @e[tag=sheep_temp,limit=1] s_thrower_id
execute at @s as @a[distance=..4] store result score @s s_sheep_type run scoreboard players get @e[tag=sheep_temp,limit=1] s_sheep_type
execute at @s run scoreboard players set @a[distance=..4] s_kill_time 0
tag @a[tag=player_temp] remove player_temp
```

相当于把本羊羊的id和类型记录到了周围被炸玩家的身上，并且把一个计时器重置到0。接着新建一个函数`minecraft:player_death/tick`高频执行来控制这个计时器，同时新建计分板标记死亡玩家。
```
execute as @a[scores={death=1..}] run function player_death/main
scoreboard players add @a s_kill_time 1
```

在`minecraft:player_death/main`这个函数中包含了玩家的死亡信息。如果说在玩家死亡的时候`s_kill_time`这个计分板刚好为0或1（我也不知道为啥有可能是1，实验测试确实可能），那就表示他是被炸死的！因此该函数的内容是：
```
#标记自己和杀人凶手
tag @s add player_self
execute as @a if score @s s_id = @a[tag=player_self,limit=1] s_thrower_id run tag @s add player_killer
#三种情况：被别人杀，被自己杀，被无名的羊杀（可能是机关）
execute if entity @s[scores={s_kill_time=..1}] if entity @a[tag=player_killer] if entity @s[tag=!player_killer] run function player_death/message/kill_by_player
execute if entity @s[scores={s_kill_time=..1}] if entity @s[tag=player_killer] run function player_death/message/kill_by_himself
execute if entity @s[scores={s_kill_time=..1}] unless entity @a[tag=player_killer] run function player_death/message/kill_by_sheep
#清除标记
scoreboard players set @s death 0
tag @s remove player_self
tag @a[tag=player_killer] remove player_killer
```

此处`minecraft:player_death/message/`下的各种函数就是死亡信息的tellraw了，大家有空可以自己看看内容。

#### 被摔死和被迫摔死

游戏内虽然用鞋子阻止了一部分摔落伤害，但仍有可能摔死。尤其是游戏内扇子的加入，让双方玩家更容易摔伤了。新建一个计分板`fall_dis`，判据是`minecraft.custom:minecraft.fall_one_cm`，表示玩家空中掉落距离。这个判据有个特点，就是在玩家落地的一瞬间才会更新，玩家在空中时不变。因此可以在玩家每次落地且没摔死之后清零。`minecraft:player_death/tick`函数变成这样：
```
execute as @a[scores={death=1..}] run function player_death/main
scoreboard players add @a death 0
scoreboard players set @a[scores={fall_dis=1..,death=0}] fall_dis 0
scoreboard players add @a s_kill_time 1
```

游戏内很多的摔死是由羊羊的爆炸造成，如果连这个都可以判定的话就太好了！如果一个玩家是被炸起来摔死的，那么他的`s_kill_time`一定不会太大，假设在2~233之间。除此之外`fall_dis`一定要超过300才可受到掉落伤害。但是又引出问题，也有可能是被炸之后自己走下悬崖啊对吧，因此加入一条函数，玩家落地后，也就是`nbt={OnGround:1b}`后，把`s_kill_time`调整到233以外，就可以避免这种情况。因此新的`minecraft:player_death/tick`函数如下：
```
#触发死亡信息函数
execute as @a[scores={death=1..}] run function player_death/main
scoreboard players add @a death 0
#落地修改s_kill_time
scoreboard players set @a[scores={s_kill_time=3..233,death=0},nbt={OnGround:1b}] s_kill_time 999
scoreboard players set @a[scores={fall_dis=1..,death=0}] s_kill_time 999
#重置掉落距离
scoreboard players set @a[scores={fall_dis=1..,death=0}] fall_dis 0
#时钟
scoreboard players add @a s_kill_time 1
```

新的`minecraft:player_death/main`函数（包括摔死）如下：
```
#标记自己和杀人凶手
tag @s add player_self
execute as @a if score @s s_id = @a[tag=player_self,limit=1] s_thrower_id run tag @s add player_killer
#三种情况：被别人杀，被自己杀，被无名的羊杀（可能是机关）
execute if entity @s[scores={s_kill_time=..1}] if entity @a[tag=player_killer] if entity @s[tag=!player_killer] run function player_death/message/kill_by_player
execute if entity @s[scores={s_kill_time=..1}] if entity @s[tag=player_killer] run function player_death/message/kill_by_himself
execute if entity @s[scores={s_kill_time=..1}] unless entity @a[tag=player_killer] run function player_death/message/kill_by_sheep
#四种情况：被别人的羊推，被自己的羊推，被无名的羊推，自己脚滑
execute if entity @s[scores={fall_dis=300..,s_kill_time=3..233}] if entity @a[tag=player_killer] if entity @s[tag=!player_killer] run function player_death/message/push_by_player
execute if entity @s[scores={fall_dis=300..,s_kill_time=3..233}] if entity @s[tag=player_killer] run function player_death/message/push_by_himself
execute if entity @s[scores={fall_dis=300..,s_kill_time=3..233}] unless entity @a[tag=player_killer] run function player_death/message/push_by_sheep
execute if entity @s[scores={fall_dis=300..,s_kill_time=999..}] run function player_death/message/fall_to_death

#清除标记
scoreboard players set @s death 0
tag @s remove player_self
tag @a[tag=player_killer] remove player_killer
```

#### 其他死因

其他死因包括被怪物锤、岩浆或淹死等，均有对应的计分板判据可确定，这里不再赘述。除此之外的其他死因如魔法伤害、仙人掌、虚空、凋零等均没有额外判定，用一个死的不明不白代替。即排除被炸死和摔死（简化教程，未考虑其他死因）：
```
execute if entity @s[scores={fall_dis=..300,s_kill_time=3..}] run function player_death/message/stupid
```

## 附录A-数据包结构

本解析的数据包结构如下：
```
sheep:
    - launch
        - main.mcfunction
    - fly
        - tick.mcfunction
        - move.mcfunction
        - split_speed.mcfunction
        - touch_x.mcfunction
        - touch_y.mcfunction
        - touch_z.mcfunction
    - blow
        - tick.mcfunction
        - main.mcfunction
    - fan
        - tick.mcfunction
        - main.mcfunction
        - player.mcfunction
    - tick.mcfunction
minecraft:
    - game_mode
        - single
            - death_match
                - pre
                    - main.mcfunction
                    - tick.mcfunction
                - start.mcfunction
                - tick.mcfunction
        - ending
            - main.mcfunction
            - tick.mcfunction
        - free
            - main.mcfunction
            - tick.mcfunction
        - public
    - player_death
        - main.mcfunction
        - tick.mcfunction
        - message
    - tick.mcfunction
```